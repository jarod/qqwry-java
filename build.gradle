buildscript {
    repositories {
        jcenter()
    }
    ext {
        junitJupiterVersion = '5.3.2'
    }
}

apply plugin: 'java-library'
apply plugin: 'eclipse'
apply plugin: 'jacoco'
apply plugin: 'maven'
apply plugin: 'signing'

sourceCompatibility = 1.8
group = 'com.github.jarod'
archivesBaseName = 'qqwry-java'
version = '0.7.2'

jacoco {
    toolVersion = "0.8.2"
}

repositories {
    jcenter()
}
dependencies {
    testImplementation(
            "org.junit.jupiter:junit-jupiter-api:${junitJupiterVersion}",
    )
    testRuntime(
            "org.junit.jupiter:junit-jupiter-engine:${junitJupiterVersion}",
    )
}

test {
    useJUnitPlatform {
        excludeTags 'slow'
    }
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.java
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: repoReleaseUrl) {
                authentication(userName: repoUsername, password: repoPassword)
            }

            snapshotRepository(url: repoSnapshotUrl) {
                authentication(userName: repoUsername, password: repoPassword)
            }

            pom.project {
                name 'qqwry-java'
                packaging 'jar'
                description 'A java library to read QQWry IP database.'
                url 'https://github.com/jarod/qqwry-java'

                scm {
                    connection 'scm:git:https://github.com/jarod/qqwry-java.git'
                    developerConnection 'scm:git:git@github.com:jarod/qqwry-java.git'
                    url 'https://github.com/jarod/qqwry-java'
                }

                licenses {
                    license {
                        name 'The MIT License'
                        url 'https://github.com/jarod/qqwry-java/blob/master/LICENSE'
                    }
                }

                developers {
                    developer {
                        id 'jarod_liu'
                        name 'Jarod Liu'
                        email 'liuyuanzhi@gmail.com'
                    }
                }
            }
        }
    }
}
